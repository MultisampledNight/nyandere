; the actual parser impl accepts a superset of this syntax, actually
; think of this syntax as a "declaration of intent"
; rather than an ultimate truth
;
; (this is a subset in the meaning that any script written in this syntax
; should also be accepted by the parser,
; the parser might accept even more though)

; toplevel
script = *(*sd (statement sd / comment) *sd)

comment = "#" *(VCHAR / hsp) newline

; commands
statement = create / transfer / analyze

create = "create" hsp actor
transfer = pay / deliver / purchase
analyze = stats / balance

pay = "pay" value dir
deliver = "deliver" product [price] dir
purchase = "purchase" product [price] dir

stats = "stats" / "statistics"
balance = ("bal" / "balance") dir

; actors
actor = entity / object / concept
entity = "entity" hsp ident
object = "object" hsp ident [hsp "parent" hsp ident]
concept = "concept" hsp ident [hsp price] [hsp "gtin" gtin]

; parameters
product = hsp (ident / gtin)
price = hsp "price" value
value = hsp money

dir = from to
from = hsp "from" hsp ident
to = hsp "to" hsp ident

; literals
money = cents / euros

cents = decimal [osp sign-ct]
euros = (decimal [osp sign-eur]) / (nat osp sign-eur)

sign-eur = "€" / "eur" / "EUR"
sign-ct = "¢" / "ct" / "cents"

; not entirely correct but eh
gtin = *14DIGIT

; basics
ident = ? Default Identifier as per https://www.unicode.org/reports/tr31/#Default_Identifier_Syntax ?

decimal = nat "." 2DIGIT
nat = 1*DIGIT

; whitespace
; Statement delimiter.
sd = osp (";" / newline) osp
newline = LF / CRLF
; Optional (can be, but doesn't have to)
osp = *WSP
; Hard (at least one)
hsp = 1*WSP

; one-character
alnum = ALPHA / DIGIT

