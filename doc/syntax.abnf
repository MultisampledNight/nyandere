; the actual parser impl accepts a superset of this syntax, actually
; think of this syntax as a "declaration of intent"
; rather than an ultimate truth
;
; (this is a subset in the meaning that any script written in this syntax
; should also be accepted by the parser,
; the parser might accept even more though)

; toplevel
script = *(*sd (statement sd / comment) *sd)

comment = "#" *(VCHAR / hsp) newline

; commands
statement = command args-positional args-keyvalue
args-positional = *value
args-keyvalue = *(hsp param hsp value)

command = ident
param = ident
value = literal / variable ; could expand to variables in future

literal = money / ratio / gtin
variable = ident

money = cents / euros

cents = nat [osp sign-ct]
euros = (decimal / nat) osp sign-eur

sign-eur = "€" / "eur" / "EUR"
sign-ct = "¢" / "ct" / "cents"

ratio = nat osp ":" osp nat

; not entirely correct but eh
gtin = 8*14DIGIT

; basics
ident = ? Default Identifier as per https://www.unicode.org/reports/tr31/#Default_Identifier_Syntax ?

decimal = nat "." 2DIGIT
nat = (nonzero *DIGIT) / "0"

; whitespace
; Statement delimiter.
sd = osp (";" / newline) osp
newline = LF / CRLF
; Optional (can be, but doesn't have to)
osp = *WSP
; Hard (at least one)
hsp = 1*WSP

; one-character
alnum = ALPHA / DIGIT
nonzero = %x31-39

